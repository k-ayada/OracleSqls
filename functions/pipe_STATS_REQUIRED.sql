
CREATE OR REPLACE FUNCTION STATS_REQUIRED(
    I_STATUS    IN  VARCHAR2    DEFAULT 'LIST AUTO',
    I_SCHEMA    IN  VARCHAR2    DEFAULT USER
)
RETURN DBMS_STATS.OBJECTTAB PIPELINED
AS
    PRAGMA AUTONOMOUS_TRANSACTION;
    M_OBJECTS   DBMS_STATS.OBJECTTAB;
    FILTER_LIST DBMS_STATS.OBJECTTAB := DBMS_STATS.OBJECTTAB ();
BEGIN
    IF UPPER(I_STATUS) NOT IN (
        'LIST AUTO', 'LIST STALE', 'LIST EMPTY'
    ) THEN
        RETURN;
    END IF;
    FILTER_LIST.EXTEND (1);
    FILTER_LIST (1).OBJTYPE   := 'TABLE'; 
    IF I_SCHEMA IS NULL THEN
        DBMS_STATS.GATHER_DATABASE_STATS(
            OPTIONS         => I_STATUS,
            OBJLIST         => M_OBJECTS,
            OBJ_FILTER_LIST => FILTER_LIST
        );
    ELSE
        DBMS_STATS.GATHER_SCHEMA_STATS(
            OWNNAME         => I_SCHEMA,
            OPTIONS         => I_STATUS,
            OBJLIST         => M_OBJECTS,
            OBJ_FILTER_LIST => FILTER_LIST
        );
    END IF;
    COMMIT;
 
    FOR I IN 1..M_OBJECTS.COUNT LOOP
        PIPE ROW(M_OBJECTS(I));
    END LOOP;
 
    RETURN;
 
END;
/

--SELECT * FROM table(STATS_REQUIRED('LIST AUTO' ,'schema'));
